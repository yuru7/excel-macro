以下の構成のVBAを用意して、Excel アドイン (.xlam) として保存する。

### Moduleとして以下を登録する。

```
Sub AddBorderLeft()
    Dim targetRange As Range
    Set targetRange = Selection
    BorderToggle targetRange, xlEdgeLeft, True
End Sub

Sub AddBorderRight()
    Dim targetRange As Range
    Set targetRange = Selection
    BorderToggle targetRange, xlEdgeRight, True
End Sub

Sub AddBorderTop()
    Dim targetRange As Range
    Set targetRange = Selection
    BorderToggle targetRange, xlEdgeTop, True
End Sub

Sub AddBorderBottom()
    Dim targetRange As Range
    Set targetRange = Selection
    BorderToggle targetRange, xlEdgeBottom, True
End Sub

Sub AddBorderInsideHorizontal()
    Dim targetRange As Range
    Set targetRange = Selection
    InsideBorderToggle targetRange, xlInsideHorizontal, True
End Sub

Sub AddBorderInsideVertical()
    Dim targetRange As Range
    Set targetRange = Selection
    InsideBorderToggle targetRange, xlInsideVertical, True
End Sub

Sub RemoveBorderLeft()
    Dim targetRange As Range
    Set targetRange = Selection
    BorderToggle targetRange, xlEdgeLeft, False
End Sub

Sub RemoveBorderRight()
    Dim targetRange As Range
    Set targetRange = Selection
    BorderToggle targetRange, xlEdgeRight, False
End Sub

Sub RemoveBorderTop()
    Dim targetRange As Range
    Set targetRange = Selection
    BorderToggle targetRange, xlEdgeTop, False
End Sub

Sub RemoveBorderBottom()
    Dim targetRange As Range
    Set targetRange = Selection
    BorderToggle targetRange, xlEdgeBottom, False
End Sub

Sub RemoveBorderInsideHorizontal()
    Dim targetRange As Range
    Set targetRange = Selection
    InsideBorderToggle targetRange, xlInsideHorizontal, False
End Sub

Sub RemoveBorderInsideVertical()
    Dim targetRange As Range
    Set targetRange = Selection
    InsideBorderToggle targetRange, xlInsideVertical, False
End Sub

Private Function BorderToggle(targetRange As Range, edge As Integer, addFlg As Boolean)
    Dim targetStyle As Integer
    Dim statusBarMsg As String

    Select Case edge
        Case xlEdgeLeft
            statusBarMsg = "左の罫線"
        Case xlEdgeRight
            statusBarMsg = "右の罫線"
        Case xlEdgeTop
            statusBarMsg = "上の罫線"
        Case xlEdgeBottom
            statusBarMsg = "下の罫線"
        Case Else
            Exit Function
    End Select

    If addFlg Then
        targetRange.Borders(edge).LineStyle = xlContinuous
        Application.StatusBar = statusBarMsg & "を追加"
    Else
        targetRange.Borders(edge).LineStyle = xlLineStyleNone
        Application.StatusBar = statusBarMsg & "を削除"
    End If
End Function

Private Function InsideBorderToggle(targetRange As Range, inside As Integer, addFlg As Boolean)
    Dim statusBarMsg As String

    If targetRange.Cells.Count < 1 Then
        Exit Function
    End If

    Select Case inside
        Case xlInsideHorizontal
            statusBarMsg = "水平の罫線"
        Case xlInsideVertical
            statusBarMsg = "垂直の罫線"
        Case Else
            Exit Function
    End Select

    If addFlg Then
        targetRange.Borders(inside).LineStyle = xlContinuous
        Application.StatusBar = statusBarMsg & "を追加"
    Else
        targetRange.Borders(inside).LineStyle = xlLineStyleNone
        Application.StatusBar = statusBarMsg & "を削除"
    End If
End Function
```

### ThisWorkbookに下記を記載する。

```
Private Sub Workbook_Open()
    Application.OnKey "^%h", "Module1.AddBorderLeft"
    Application.OnKey "^%j", "Module1.AddBorderBottom"
    Application.OnKey "^%k", "Module1.AddBorderTop"
    Application.OnKey "^%l", "Module1.AddBorderRight"
    Application.OnKey "^%n", "Module1.AddBorderInsideVertical"
    Application.OnKey "^%m", "Module1.AddBorderInsideHorizontal"
    Application.OnKey "+^%h", "Module1.RemoveBorderLeft"
    Application.OnKey "+^%j", "Module1.RemoveBorderBottom"
    Application.OnKey "+^%k", "Module1.RemoveBorderTop"
    Application.OnKey "+^%l", "Module1.RemoveBorderRight"
    Application.OnKey "+^%n", "Module1.RemoveBorderInsideVertical"
    Application.OnKey "+^%m", "Module1.RemoveBorderInsideHorizontal"
End Sub
```